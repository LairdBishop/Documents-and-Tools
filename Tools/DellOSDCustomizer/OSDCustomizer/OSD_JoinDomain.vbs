' //******************************************************************************************************************
' // Author: 	Amar Maouche - Dell IMS
' // Version:		30.06.2016
' //
' // Requirement: - This script has to be called by main script OSDCustomizer.vbs. 
' // 			  - This script is using OSDProfile.ini as input. OSDprofile.ini is generated by calling the OSDcustomizer.vbs
' //			
' //*******************************************************************************************************************

	
	Const ForReading = 1
	Const ForWriting = 2
	Const ForAppending = 8
	
	'Max Join domain retry
	Const MAX_RETRY = 5
	
	' Constants to choose from when joining
	Const JOIN_DOMAIN = 1
	Const ACCT_CREATE = 2
	Const ACCT_DELETE = 4
	Const WIN9X_UPGRADE = 16
	Const DOMAIN_JOIN_IF_JOINED = 32
	Const JOIN_UNSECURE = 64
	Const MACHINE_PASSWORD_PASSED = 128
	Const DEFERRED_SPN_SET = 256
	Const INSTALL_INVOCATION = 262144
	
	Dim sJoin,sDomain,sDomainOU,sDomainUser,sDomainUserPassword
	Dim objFSO,s_ScriptDir,s_ScriptName,LogFile,sOSDProfileIniFile
	Dim vJoinDomain
	vJoinDomain=False

	On Error Resume Next
	Set objFSO = WScript.CreateObject("Scripting.FileSystemObject")
	' set the script directory
	s_ScriptDir = WScript.ScriptFullName
	s_ScriptName= objFSO.GetFileName(s_ScriptDir)
	s_ScriptDir = Left(s_ScriptDir, InStrRev(s_ScriptDir, "\"))
	
	LogFile=s_ScriptDir & s_ScriptName & ".log"
	sOSDProfileIniFile =  s_ScriptDir & "OSDProfile.ini"
	
	'get settings from osdprofile.ini
	getOSDSelectionConfig
	
	'run join domain
	If vJoinDomain=True Then 
		JoinToDomain sDomain,sDomainOU,sDomainUser,sDomainUserPassword
	else
		WriteLog "No Join to domain. Check OSDProfile.ini for more details."
	end if

Sub getOSDSelectionConfig
	Dim sSelSection
	Dim  i
  
	if objFSO.FileExists(sOSDProfileIniFile) = True Then
	
		WriteLog "Getting Domain join settings from OSDProfile.ini file..."
		
		'read  the main selected section
			sSelSection=Trim(ReadIni(sOSDProfileIniFile,"Main","Selected"))
			WriteLog "Selected=" & sSelSection
			
		'get Properties
			
		sJoin=Trim(ReadIni(sOSDProfileIniFile,sSelSection,"Join"))

		if ucase(trim(sJoin))= ucase("Workgroup") then
			WriteLog "Join to Workgroup is selected."
			vJoinDomain=False
			
		elseIf ucase(trim(sJoin))= ucase("Domain") then

			sDomain=Trim(ReadIni(sOSDProfileIniFile,sSelSection,"Domain"))
			If Len(sDomain) > 0 Then 
				WriteLog "Domain=" & sDomain
			else
				WriteLog "No value found for Domain." 
			end if

			sDomainOU=Trim(ReadIni(sOSDProfileIniFile,sSelSection,"DomainOU"))
			If Len(sDomainOU) > 0 Then 
				WriteLog "DomainOU=" & sDomainOU
			end if

			sDomainUser=Trim(ReadIni(sOSDProfileIniFile,sSelSection,"DomainUser"))
			If Len(sDomainUser) > 0 Then 
				WriteLog "DomainUser=" & sDomainUser
			else
				WriteLog "No value found for DomainUser." 
			end if

			sDomainUserPassword=Trim(ReadIni(sOSDProfileIniFile,sSelSection,"DomainUserPassword"))
			If Len(sDomainUserPassword) > 0 Then 
				WriteIni sOSDProfileIniFile, sSelSection, "DomainUserPassword", "<Passowrd removed for security reason>"		
			else
				WriteLog "No value found for DomainUserPassword."
			end if
			
			If Len(sDomain) > 0 AND Len(sDomainUser) > 0 AND Len(sDomainUserPassword) > 0 Then 
				vJoinDomain=True		
			else
				vJoinDomain=False
				WriteLog "One or more values missing in OSDProfile.ini for a Join to Domain."
			end if
		else
			WriteLog "No values found in OSDProfile.ini for a Join to Domain."
			vJoinDomain=False
		end if
	Else
	
		WriteLog "Error: OSDProfile.ini file not found."
		vJoinDomain=False
	End If
			
End Sub

Sub JoinToDomain(strDomain,strOU,strUser,strPassword)
	Dim Status, iCounter, iMaxRetry
	
	Set objNetwork = CreateObject("WScript.Network")
	strComputer = objNetwork.ComputerName
	If Not InStr (1, strUser, "\", 1) > 0 Then
	
		If Not InStr (1, strUser, "@", 1) > 0 Then
				If Instr(1, strDomain, ".", 1) > 0 Then
					strUser=strUser & "@" & strDomain
				Else
					strUser=strDomain & "\" & strUser
				End If
			
		End If
		
	End If
	
	' Join Domain
	Set objComputer = GetObject("winmgmts:{impersonationLevel=Impersonate}!\\" & _
	strComputer & "\root\cimv2:Win32_ComputerSystem.Name='" & _
	strComputer & "'")
	
	If Len(strOU)=0 Then
		strOU="NULL"
	End If
	
	WriteLog "Start Join domain using command :" & "JoinDomainOrWorkGroup(" & strDomain & "," & "<Passowrd removed for security reason>" & "," & strUser &"," & strOU & "," & JOIN_DOMAIN + ACCT_CREATE & ")"
	
	iCounter=0
	
	Do While iCounter < MAX_RETRY
			
			ReturnValue = objComputer.JoinDomainOrWorkGroup( _
								strDomain, _
								strPassword, _
								strUser, _
								strOU, _
								JOIN_DOMAIN + ACCT_CREATE) 'Join + create account
			
			If ReturnValue = 2224 Then
				WriteLog "JoinDomain Failure: The computer account *may* already exist. Retrying without account creation."
				ReturnValue = objComputer.JoinDomainOrWorkGroup( _
						strDomain, _
						strPassword, _
						strUser, _
						strOU, _
						JOIN_DOMAIN) ' Join only

				WriteLog "Retry attempt: JoinDomain(" & strDomain & ",<Passowrd removed for security reason>," & strUser & ",," & JOIN_DOMAIN & ")"						
			End if
			
						
			Select Case ReturnValue
				Case 0 Status = "Success"
				Case 2 Status = "Missing OU"
				Case 5 Status = "Access denied"
				Case 53 Status = "Network path not found"
				Case 87 Status = "Parameter incorrect"
				Case 1326 Status = "Logon failure, user or pass"
				Case 1355 Status = "Domain can not be contacted"
				Case 1909 Status = "User account locked out"
				Case 2224 Status = "Computer Account allready exists"
				Case 2691 Status = "Allready joined"
				Case Else Status = "UNKNOWN ERROR " & ReturnValue
			
			End Select
			
			If ReturnValue = 0 then
				WriteLog "JoinDomain Succeeded"
				Exit Do 			
			ElseIf ReturnValue = 2691 then
				WriteLog"Machine is already a member of the domain."
				Exit Do 
			Else
				iCounter= iCounter + 1
				If iCounter < MAX_RETRY Then 
					WriteLog "Retrying in 2 seconds..."
					WScript.Sleep 3000
				End If 
			End If
	Loop
	
	
	If Status <> "Success" Then
		WriteLog "Error: Join domain failed with status: " & Status
	End If
	
End Sub 

Function WriteLog(sLogMsg)

		Dim sTime, sDate, sTempMsg, oLog, oConsole

		'On error resume next		
		' Suppress messages containing password
		If not strDebug then
			If Instr(1, sLogMsg, "password", 1) > 0 then
			'	sLogMsg = "<Message containing password has been suppressed>"
			End if
		End if

		' Populate the variables to log
			sTempMsg = "[LOG]:" & " Date: " & Date & " " & Time  & " Message: " &  sLogMsg
			
		' If debug, echo the message
		If strDebug then
			Set oConsole = objFSO.GetStandardStream(1) 
			oConsole.WriteLine sLogMsg
		End if

		' Create the log entry
		Err.Clear
		Set oLog = objFSO.OpenTextFile(LogFile, ForAppending, True)
		
		If Err then
			Err.Clear
			Exit Function
		End if
		oLog.WriteLine sTempMsg
		oLog.Close
		Err.Clear

End Function

Function ReadIni(file, section, item)

		Dim line, equalpos, leftstring, ini

		ReadIni = ""
		file = Trim(file)
		item = Trim(item)

		On Error Resume Next
		Set ini = objFSO.OpenTextFile( file, 1, False)
		If Err then
			Err.Clear
			Exit Function
		End if
		On Error Goto 0

		Do While (not ini.AtEndOfStream)
			line = ini.ReadLine
			line = Trim(line)
			If LCase(line) = "[" & LCase(section) & "]" and (not ini.AtEndOfStream) Then
				line = ini.ReadLine
				line = Trim(line)
				Do While Left( line, 1) <> "["
					'If InStr( 1, line, item & "=", 1) = 1 Then
					equalpos = InStr(1, line, "=", 1 )
					If equalpos > 0 Then
						leftstring = Left(line, equalpos - 1 )
						leftstring = Trim(leftstring)
						If LCase(leftstring) = LCase(item) Then
							ReadIni = Mid( line, equalpos + 1 )
							ReadIni = Trim(ReadIni)
							Exit Do
						End If
					End If

					If ini.AtEndOfStream Then Exit Do
					line = ini.ReadLine
					line = Trim(line)
				Loop
				Exit Do
			End If
		Loop
		ini.Close

End Function

' WRITEINI ( file, section, item, myvalue )
' file = path and name of ini file
' section = [Section] must not be in brackets
' item = the variable to write;
' myvalue = the myvalue to assign to the item.
'
Sub WriteIni( file, section, item, myvalue )
Dim in_section, section_exists, item_exists, wrote
Dim itemtrimmed, temp_ini, read_ini, write_ini
Dim line, linetrimmed, equalpos, TristateFalse, leftstring 
	
	TristateFalse=0
	in_section = False
	section_exists = False
	item_exists = ( ReadIni( file, section, item ) <> "" )
	
	wrote = False
	file = Trim(file)
	itemtrimmed = Trim(item)
	myvalue = Trim(myvalue)

	temp_ini = s_ScriptDir & objFSO.GetTempName

	Set read_ini = objFSO.OpenTextFile( file, 1, True, TristateFalse )
	Set write_ini = objFSO.CreateTextFile( temp_ini, False)

	While read_ini.AtEndOfStream = False
		line = read_ini.ReadLine
		linetrimmed = Trim(line)
		If wrote = False Then
			If LCase(line) = "[" & LCase(section) & "]" Then
				section_exists = True
				in_section = True
			ElseIf InStr( line, "[" ) = 1 Then
				in_section = False
			End If
		End If

		If in_section Then
			If item_exists = False Then
				write_ini.WriteLine line
				write_ini.WriteLine item & "=" & myvalue
				wrote = True
				in_section = False
			Else
				equalpos = InStr(1, line, "=", 1 )
				If equalpos > 0 Then
					leftstring = Left(line, equalpos - 1 )
					leftstring = Trim(leftstring)
					If LCase(leftstring) = LCase(item) Then
						write_ini.WriteLine itemtrimmed & "=" & myvalue
						wrote = True
						in_section = False
					End If
				End If
				If Not wrote Then
					write_ini.WriteLine line
				End If
			End If
		Else
				equalpos = InStr(1, line, "=", 1 )
				If equalpos > 0 Then
					leftstring = Left(line, equalpos - 1 )
					leftstring = Trim(leftstring)
					rightsring=Right (line, Len(line) - (Len(leftstring)+1) )
					If LCase(leftstring) = LCase(item) then
						If Len(rightsring) =0  Then
							'do nothing
						End if
					Else
						write_ini.WriteLine line
					End If
				Else
					write_ini.WriteLine line
				End If
			
		End If
	Wend

	If section_exists = False Then ' section doesn't exist
		write_ini.WriteLine
		write_ini.WriteLine "[" & section & "]"
		write_ini.WriteLine itemtrimmed & "=" & myvalue

	End If

	read_ini.Close
	write_ini.Close
	If objFSO.FileExists(file) then
		objFSO.DeleteFile file, True
	End if
	objFSO.CopyFile temp_ini, file, true
	objFSO.DeleteFile temp_ini, True

End Sub